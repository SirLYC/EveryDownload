import groovy.json.JsonBuilder
import groovy.json.JsonSlurper

import java.security.MessageDigest

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

static String generateMD5(File file) {
    if (!file.exists() || !file.isFile()) {
        return null
    }
    def digest = MessageDigest.getInstance("MD5")
    file.withInputStream() { is ->
        byte[] buffer = new byte[8192]
        int read
        while ((read = is.read(buffer)) > 0) {
            digest.update(buffer, 0, read)
        }
    }
    return digest.digest().encodeHex().toString()
}

void generateUpdateInfo(String apkName) {
    println("------------------ Generating version info ------------------")
    def apkFile = project.file("build/outputs/apk/release/$apkName")
    if (!apkFile.exists()) {
        throw new GradleScriptException("apk file not exist!")
    }
    def toDir = rootProject.file(buildInfo.updatePath)
    String apkHash = generateMD5(apkFile)
    def updateJsonFile = new File(toDir, buildInfo.updateInfoFilename)
    def writeNewFile = true
    if (updateJsonFile.exists()) {
        try {
            def oldUpdateInfo = new JsonSlurper().parse(updateJsonFile)
            if (buildInfo.versionCode <= oldUpdateInfo.code && apkHash == oldUpdateInfo.hash) {
                writeNewFile = false
            }
        } catch (Exception e) {
            writeNewFile = true
            e.printStackTrace()
            updateJsonFile.delete()
        }
    }

    if (writeNewFile) {
        def oldFiles = toDir.listFiles()
        oldFiles.each {
            if (!it.delete()) {
                it.deleteOnExit()
            }
        }
        copy {
            from(apkFile)
            into(toDir)
        }
        def updateInfo = new Expando(
                code: buildInfo.versionCode,
                name: buildInfo.versionName,
                filename: apkFile.name,
                url: "${buildInfo.updateBaseUrl}${apkFile.name}",
                time: System.currentTimeMillis(),
                des: buildInfo.versionDes,
                size: apkFile.length(),
                md5: apkHash
        )
        String newApkHash = generateMD5(new File(toDir, apkName))
        println("new apk md5: $newApkHash")
        def outputJson = new JsonBuilder(updateInfo).toPrettyString()
        println(outputJson)
        updateJsonFile.write(outputJson)
    } else {
        println("This version is already released.\n" +
                "VersionCode = ${buildInfo.versionCode}\n" +
                "Skip generateUpdateInfo.")
    }
    println("------------------ Finish Generating version info ------------------")
}

android {
    compileSdkVersion 28
    defaultConfig {
        applicationId "com.lyc.everydownload"
        minSdkVersion buildInfo.minSdkVersion
        targetSdkVersion buildInfo.targetSdkVersion
        versionCode buildInfo.versionCode
        versionName buildInfo.versionName
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }


    signingConfigs {
        release {
            def signConfig = rootProject.file('keys/keystore.properties')
            if (signConfig.exists()) {
                Properties props = new Properties()
                props.load(new FileInputStream(signConfig))
                storeFile file(props['KEYSTORE_FILE'])
                storePassword props['KEYSTORE_PWD']
                keyAlias props['KEY_ALIAS']
                keyPassword props['KEY_PWD']
            }
        }
    }

    buildTypes.all { buildType ->
        buildType.buildConfigField("String", "RAW_URL", "\"${buildInfo.updateBaseUrl}\"")
        buildType.buildConfigField("String", "APK_NAME", "\"${"EveryDownload-${buildType.name}-${defaultConfig.versionName}.apk"}\"")
        buildType.buildConfigField("String", "INFO_FILE_NAME", "\"${buildInfo.updateInfoFilename}\"")
    }

    buildTypes {
        debug {
            applicationIdSuffix ".debug"
        }

        release {
            minifyEnabled true
            shrinkResources true
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    applicationVariants.all { variant ->
        def apkName = "EveryDownload-${variant.buildType.name}-${defaultConfig.versionName}.apk"
        variant.outputs.all {
            outputFileName = apkName
        }
        if (variant.buildType.name == "release") {
            variant.assembleProvider.get().doLast {
                generateUpdateInfo(apkName)
            }
        }
    }

    compileOptions {
        targetCompatibility = "8"
        sourceCompatibility = "8"
    }
}


dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation deps['downloader']
    implementation deps['appcompact']
    implementation deps['core']
    implementation deps['preference']
    implementation deps['material']
    implementation deps['constraintlayout']
    implementation deps['lifecycle-extensions']
    implementation deps['recyclerview']
    implementation deps['core-ktx']
    implementation deps['multi-type']
    implementation deps['lifecycle-ktx']
    implementation deps['kotlin-stdlib']
    implementation deps['rxpermissions']
    implementation deps['rxjava']

    testImplementation deps['junit']
}
